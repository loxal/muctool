/*
 * MUCtool Web Toolkit
 *
 * Copyright 2018 Alexander Orlov <alexander.orlov@loxal.net>. All rights reserved.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        ktor_version = "0.9.5"
    }
}
// Migrate to Kotlin https://guides.gradle.org/migrating-build-logic-from-groovy-to-kotlin/
plugins {
    id "application"
    id "idea"
    id "org.jetbrains.kotlin.jvm" version "1.2.71"
    id "io.morethan.jmhreport" version "0.8.0"
}

group = "net.loxal.muctool"
version = "1.0.0"
description = "MUCtool Web Toolkit Goodness"

//sourceCompatibility = JavaVersion.VERSION_1_10
//targetCompatibility = JavaVersion.VERSION_1_10
sourceCompatibility = "11"
targetCompatibility = "11"

String serverEngine = "netty" // or "jetty"

mainClassName = "io.ktor.server.${serverEngine}.DevelopmentEngine"

repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2" }
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

task singleJar(type: Jar) {
    manifest {
        attributes(
                "Main-Class": mainClassName
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } with jar
}

jmhReport {
    jmhResultPath = "$buildDir/jmh-result.json"
    jmhReportOutput = buildDir
}
task runJMH(type: JavaExec) {
    main = "net.loxal.muctool.jmh.LoadBenchmark"
    classpath sourceSets.test.runtimeClasspath
}
tasks.runJMH.finalizedBy tasks.jmhReport

dependencies {
    compile "com.squareup.okhttp3:okhttp:3.11.0"
    compile "com.maxmind.geoip2:geoip2:2.12.0"
    compile "ch.qos.logback:logback-classic:1.2.3"

    compile "org.http4k:http4k-client-jetty:3.26.6"

    compile "io.ktor:ktor-locations:$ktor_version"
    compile "io.ktor:ktor-websockets:$ktor_version"
    compile "io.ktor:ktor-server-${serverEngine}:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testAnnotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:1.21"
    testCompile "org.openjdk.jmh:jmh-core:1.21"
    
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
}
